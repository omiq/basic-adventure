1 REM TEXT ADVENTURE GAME BY CHRIS GARRETT 2024 RETROGAMECODERS.COM
2 REM SET UP VARIABLES ETC
3 GOSUB 128
4 REM SHOW ROOM DETAILS
5 GOSUB 126
6 IF PL=0 THEN PL = PP : REM PLAYER LOCATION CAN NOT BE 00 AS THAT IS INVENTORY
7 PP = PL : REM BACKUP THE LOCATION IN CASE ILLEGAL MOVE MADE
8 PRINT RV$+LO$(PL)+RO$
9 PRINT ""
10 PRINT "OBJECTS VISIBLE:"+LB$
11 FOR I = 0 TO OC-1 : REM CHECK OBJECT LOCATIONS FROM THE FIRST OBJECT TO OBJECT COUNT
12 IF OL(I) = PL THEN PRINT ". ";OB$(I) : REM IF THE OBJECT IS IN CURRENT LOCATION PRINT IT
13 NEXT I
14 PRINT ""
15 PRINT WT$+"EXITS AVAILABLE:"+LB$
16 REM CHECK EACH POSSIBLE EXIT
17 IF MID$(EX$(PL),1,2)<>"00" THEN PRINT ". NORTH"
18 IF MID$(EX$(PL),3,2)<>"00" THEN PRINT ". EAST"
19 IF MID$(EX$(PL),5,2)<>"00" THEN PRINT ". SOUTH"
20 IF MID$(EX$(PL),7,2)<>"00" THEN PRINT ". WEST"
21 IF MID$(EX$(PL),9,2)<>"00" THEN PRINT ". UP"
22 IF MID$(EX$(PL),11,2)<>"00" THEN PRINT ". DOWN"
23 I$=""
24 PRINT ""
25 PRINT YL$+"WHAT NOW?"+LB$
26 INPUT I$
27 IF LEFT$(I$,3) = "GO " THEN GOSUB 42
28 IF I$ = "N" THEN GOSUB 46
29 IF I$ = "E" THEN GOSUB 46
30 IF I$ = "S" THEN GOSUB 46
31 IF I$ = "W" THEN GOSUB 46
32 IF I$ = "U" THEN GOSUB 46
33 IF I$ = "D" THEN GOSUB 46
34 IF LEFT$(I$,1) = "I" THEN GOSUB 58
35 IF LEFT$(I$,4) = "GET " THEN GOSUB 73
36 IF LEFT$(I$,5) = "TAKE " THEN GOSUB 69
37 IF LEFT$(I$,5) = "DROP " THEN GOSUB 93
38 IF LEFT$(I$,8) = "EXAMINE " THEN GOSUB 107
39 IF LEFT$(I$,4) = "LOOK" THEN PRINT"":PRINT RD$(PL):PRINT"":GOSUB 65
40 IF LEFT$(I$,1) = "Q" THEN GOTO 190
41 GOTO 4
42 REM FULLY WRITTEN OUT MOVE (EG. GO SOUTH OR GO S)
43 D$ = MID$(I$,4,1)
44 GOSUB 50
45 RETURN
46 REM ABBREVIATED MOVE (EG. N)
47 D$ = I$
48 GOSUB 50
49 RETURN
50 REM GO TO THE NEW PLAYER LOCATION (PL)
51 IF D$ = "N" THEN PL = VAL(MID$(EX$(PL),1,2))
52 IF D$ = "E" THEN PL = VAL(MID$(EX$(PL),3,2))
53 IF D$ = "S" THEN PL = VAL(MID$(EX$(PL),5,2))
54 IF D$ = "W" THEN PL = VAL(MID$(EX$(PL),7,2))
55 IF D$ = "U" THEN PL = VAL(MID$(EX$(PL),9,2))
56 IF D$ = "D" THEN PL = VAL(MID$(EX$(PL),11,2))
57 RETURN
58 REM OBJECTS THE PLAYER IS CARRYING
59 PRINT ""
60 PRINT "OBJECTS IN YOUR POSSESSION:"
61 FOR I = 0 TO OC-1 : REM CHECK OBJECT LOCATION FROM THE FIRST OBJECT TO OBJECT COUNT
62 IF OL(I) = 0 THEN PRINT ". ";OB$(I) : REM IF THE OBJECT IS IN ZERO PRINT IT
63 NEXT I
64 PRINT ""
65 PRINT CY$+RV$+" PRESS A KEY TO CONTINUE "+RO$
66 GET I$
67 IF I$="" GOTO 66
68 RETURN
69 REM ALTERNATIVE ACTION TO GET
70 F=-1:R$=""
71 R$ = MID$(I$,6) : REM R$ IS OBJECT REQUESTED
72 GOTO 76
73 REM ALLOW PLAYER TO GET AVAILABLE OBJECT AND PUT IN INVENTORY
74 F=-1:R$=""
75 R$ = MID$(I$,5) : REM R$ IS OBJECT REQUESTED
76 REM GET THE OBJECT ID
77 FOR I = 1 TO OC
78 IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
79 NEXT I
80 REM CAN'T FIND IT?
81 PRINT ""
82 IF F=-1 THEN PRINT "CAN'T SEE THAT HERE, CHECK SPELLING AND BE SPECIFIC?" : GOTO 90
83 IF OL(F-1)=PL THEN GOTO 87
84 IF OL(F-1)=0 THEN PRINT "YOU ALREADY HAVE THAT" : GOTO 90
85 PRINT "I CAN'T SEE THAT AROUND HERE"
86 GOTO 90
87 OL(F-1)=0 : REM SET THE OBJECT LOCATION TO INVENTORY AKA ROOM ZERO
88 PRINT ""
89 PRINT "GOT THE ";OBJ$(F-1)
90 PRINT ""
91 GOSUB 65
92 RETURN
93 REM DROP OBJECTS THE PLAYER IS CARRYING
94 F=-1:R$=""
95 R$ = MID$(I$,6) : REM R$ IS OBJECT REQUESTED
96 REM GET THE OBJECT ID
97 FOR I = 1 TO OC
98 IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
99 NEXT I
100 REM CAN'T FIND IT?
101 PRINT ""
102 IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO 105
103 IF OL(F-1)=0 THEN PRINT "OK, DROPPED!" : OL(F-1)=PL : GOTO 105
104 PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"
105 GOSUB 65
106 RETURN
107 REM EXAMINE OBJECTS THE PLAYER IS CARRYING
108 F=-1:R$=""
109 R$ = MID$(I$,9) : REM R$ IS OBJECT REQUESTED
110 REM GET THE OBJECT ID
111 FOR I = 1 TO OC
112 IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
113 NEXT I
114 REM CAN'T FIND IT?
115 PRINT ""
116 IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO 119
117 IF OL(F-1)=0 THEN PRINT OD$(F-1) : GOTO 119
118 PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"
119 GOSUB 65
120 RETURN
121 REM CLEAR SCREEN AND SET UP COLOURS
122 POKE 53281,6 : POKE 53280,6: REM SCREEN COLOURS
123 LB$=CHR$(154): WT$=CHR$(5): REM LIGHT BLUE, WHITE
124 YL$=CHR$(158): CY$=CHR$(159): REM YELLOW AND CYAN
125 RV$=CHR$(18): RO$=CHR$(146): BL$=CHR$(13) + CHR$(187) + CHR$(32): REM REVERSE ON AND OFF TO MAKE LISTING THE CODE EASIER
126 PRINT WT$: PRINT CHR$(147): PRINT CHR$(19);: REM CLEAR SCREEN, WHITE TEXT
127 RETURN
128 REM SET UP THE GAME AND THE DISPLAY
129 GOSUB 121
130 REM OBJECTS AND LOCATIONS
131 REM =====================
132 REM OBJECTS
133 OC = 2 : REM OBJECT COUNT
134 DIM OB$(OC)
135 OB$(0)="MATCHES"
136 OB$(1)="BRASS KEY"
137 REM OBJECT DESCRIPTIONS
138 DIM OD$(OC)
139 OD$(0)="A SMALL BOOK OF PROMOTIONAL MATCHES ADVERTISING PATTY'S BAR AND GRILL, NORTH LAKES"
140 OD$(1)="A LARGE AND HEAVY KEY MADE OUT OF BRASS."
141 REM LOCATIONS
142 RC = 3 : REM ROOM COUNT
143 DIM LO$(RC)
144 LO$(0)="INVENTORY"
145 LO$(1)="DANK BASEMENT"
146 LO$(2)="FURNACE ROOM"
147 LO$(3)="SERVICE HATCH"
148 REM ROOM DESCRIPTIONS
149 DIM RD$(RC)
150 RD$(0)=""
151 RD$(1)="A CHILLINGLY DAMP, BARE-BRICKED ROOM WITH POURED CEMENT FLOOR AND TIMBER BEAMED CEILING. WINDOW FRAMES ARE BOARDED ALONG ONE WALL."
152 RD$(2)="THIS ROOM IS OBVIOUSLY A LATER ADDITION, THROWN TOGETHER WITH DRYWALL, AND JUST LARGE ENOUGH TO SECTION OFF THE FURNACE FROM THE MAIN BASEMENT."
153 RD$(3)="UP ABOVE THE FURNACE, THIS TINY SPACE MUST HAVE BEEN BUILT TO ALLOW ACCESS TO HVAC DUCTING."
154 REM OBJECT'S LOCATIONS
155 REM LOC 0 = PLAYER'S INVENTORY
156 DIM OL(OC)
157 OL(0)=0
158 OL(1)=1
159 REM EXIT NAMES
160 DIM EN$(6)
161 EN$(0)="NORTH"
162 EN$(1)="EAST"
163 EN$(2)="SOUTH"
164 EN$(3)="WEST"
165 EN$(4)="UP"
166 EN$(5)="DOWN"
167 REM ROOM EXITS
168 REM N E S W U D
169 DIM EX$(20)
170 EX$(1)="000002000000"
171 EX$(2)="010000000300"
172 EX$(3)="000000000002"
173 REM PLAYER
174 REM ======
175 PL = 1 : REM PLAYER LOCATION
176 PP = 1 : REM PLAYER PREVIOUS LOCATION
177 GOSUB 121
178 PRINT " MURDER HOUSE"
179 PRINT " A TEXT ADVENTURE GAME"
180 PRINT " BY CHRIS GARRETT"
181 PRINT " 2024"
182 PRINT""
183 PRINT LG$
184 PRINT " RETROGAMECODERS.COM"
185 PRINT""
186 PRINT""
187 PRINT""
188 GOSUB 65
189 RETURN
190 GOSUB 126
191 POKE 53281,6 : POKE 53280,14
192 PRINT "GOODBYE!"
193 PRINT ""
194 PRINT FRE(0)
195 PRINT ""