1 REM TEXT ADVENTURE GAME BY CHRIS GARRETT 2024 RETROGAMECODERS.COM
2 GOSUB 118
3 IF PL=0 THEN PL = PP : REM PLAYER LOCATION CAN NOT BE 00 AS THAT IS INVENTORY
4 PP = PL : REM BACKUP THE LOCATION IN CASE ILLEGAL MOVE MADE
5 PRINT "{CLR}{WHITE}{RVS ON}";LO$(PL);" {RVS OFF}"
6 PRINT ""
7 PRINT "{WHITE}OBJECTS VISIBLE:{LIGHTBLUE}"
8 FOR I = 0 TO OC-1 : REM CHECK OBJECT LOCATIONS FROM THE FIRST OBJECT TO OBJECT COUNT
9 IF OL(I) = PL THEN PRINT ". ";OB$(I) : REM IF THE OBJECT IS IN CURRENT LOCATION PRINT IT
10 NEXT I
11 PRINT ""
12 PRINT "{WHITE}EXITS AVAILABLE:{LIGHTBLUE}"
13 REM CHECK EACH POSSIBLE EXIT
14 IF MID$(EX$(PL),1,2)<>"00" THEN PRINT ". NORTH"
15 IF MID$(EX$(PL),3,2)<>"00" THEN PRINT ". EAST"
16 IF MID$(EX$(PL),5,2)<>"00" THEN PRINT ". SOUTH"
17 IF MID$(EX$(PL),7,2)<>"00" THEN PRINT ". WEST"
18 IF MID$(EX$(PL),9,2)<>"00" THEN PRINT ". UP"
19 IF MID$(EX$(PL),11,2)<>"00" THEN PRINT ". DOWN"
20 I$=""
21 PRINT ""
22 PRINT "{YELLOW}WHAT NOW?{LIGHTBLUE}"
23 INPUT I$
24 IF LEFT$(I$,3) = "GO " THEN GOSUB 39
25 IF I$ = "N" THEN GOSUB 43
26 IF I$ = "E" THEN GOSUB 43
27 IF I$ = "S" THEN GOSUB 43
28 IF I$ = "W" THEN GOSUB 43
29 IF I$ = "U" THEN GOSUB 43
30 IF I$ = "D" THEN GOSUB 43
31 IF LEFT$(I$,1) = "I" THEN GOSUB 55
32 IF LEFT$(I$,4) = "GET " THEN GOSUB 70
33 IF LEFT$(I$,5) = "TAKE " THEN GOSUB 66
34 IF LEFT$(I$,5) = "DROP " THEN GOSUB 90
35 IF LEFT$(I$,8) = "EXAMINE " THEN GOSUB 104
36 IF LEFT$(I$,4) = "LOOK" THEN PRINT"":PRINT RD$(PL):PRINT"":GOSUB 62
37 IF LEFT$(I$,1) = "Q" THEN GOTO 179
38 GOTO 3
39 REM FULLY WRITTEN OUT MOVE (EG. GO SOUTH OR GO S)
40 D$ = MID$(I$,4,1)
41 GOSUB 47
42 RETURN
43 REM ABBREVIATED MOVE (EG. N)
44 D$ = I$
45 GOSUB 47
46 RETURN
47 REM GO TO THE NEW PLAYER LOCATION (PL)
48 IF D$ = "N" THEN PL = VAL(MID$(EX$(PL),1,2))
49 IF D$ = "E" THEN PL = VAL(MID$(EX$(PL),3,2))
50 IF D$ = "S" THEN PL = VAL(MID$(EX$(PL),5,2))
51 IF D$ = "W" THEN PL = VAL(MID$(EX$(PL),7,2))
52 IF D$ = "U" THEN PL = VAL(MID$(EX$(PL),9,2))
53 IF D$ = "D" THEN PL = VAL(MID$(EX$(PL),11,2))
54 RETURN
55 REM OBJECTS THE PLAYER IS CARRYING
56 PRINT ""
57 PRINT "OBJECTS IN YOUR POSSESSION:"
58 FOR I = 0 TO OC-1 : REM CHECK OBJECT LOCATION FROM THE FIRST OBJECT TO OBJECT COUNT
59 IF OL(I) = 0 THEN PRINT ". ";OB$(I) : REM IF THE OBJECT IS IN ZERO PRINT IT
60 NEXT I
61 PRINT ""
62 PRINT "{CYAN}{RVS ON} PRESS A KEY TO CONTINUE {RVS OFF}"
63 GET I$
64 IF I$="" GOTO 63
65 RETURN
66 REM ALTERNATIVE ACTION TO GET
67 F=-1:R$=""
68 R$ = MID$(I$,6) : REM R$ IS OBJECT REQUESTED
69 GOTO 73
70 REM ALLOW PLAYER TO GET AVAILABLE OBJECT AND PUT IN INVENTORY
71 F=-1:R$=""
72 R$ = MID$(I$,5) : REM R$ IS OBJECT REQUESTED
73 REM GET THE OBJECT ID
74 FOR I = 1 TO OC
75 IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
76 NEXT I
77 REM CAN'T FIND IT?
78 PRINT ""
79 IF F=-1 THEN PRINT "CAN'T SEE THAT HERE, CHECK SPELLING AND BE SPECIFIC?" : GOTO 87
80 IF OL(F-1)=PL THEN GOTO 84
81 IF OL(F-1)=0 THEN PRINT "YOU ALREADY HAVE THAT" : GOTO 87
82 PRINT "I CAN'T SEE THAT AROUND HERE"
83 GOTO 87
84 OL(F-1)=0 : REM SET THE OBJECT LOCATION TO INVENTORY AKA ROOM ZERO
85 PRINT ""
86 PRINT "GOT THE ";OBJ$(F-1)
87 PRINT ""
88 GOSUB 62
89 RETURN
90 REM DROP OBJECTS THE PLAYER IS CARRYING
91 F=-1:R$=""
92 R$ = MID$(I$,6) : REM R$ IS OBJECT REQUESTED
93 REM GET THE OBJECT ID
94 FOR I = 1 TO OC
95 IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
96 NEXT I
97 REM CAN'T FIND IT?
98 PRINT ""
99 IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO 102
100 IF OL(F-1)=0 THEN PRINT "OK, DROPPED!" : OL(F-1)=PL : GOTO 102
101 PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"
102 GOSUB 62
103 RETURN
104 REM EXAMINE OBJECTS THE PLAYER IS CARRYING
105 F=-1:R$=""
106 R$ = MID$(I$,9) : REM R$ IS OBJECT REQUESTED
107 REM GET THE OBJECT ID
108 FOR I = 1 TO OC
109 IF OB$(I-1) = R$ THEN F=I : REM IT EXISTS
110 NEXT I
111 REM CAN'T FIND IT?
112 PRINT ""
113 IF F=-1 THEN PRINT "CAN'T SEEM TO FIND THAT, CHECK SPELLING AND BE SPECIFIC?" : GOTO 116
114 IF OL(F-1)=0 THEN PRINT OD$(F-1) : GOTO 116
115 PRINT "NO CAN DO, ARE YOU SURE YOU HAVE THAT?"
116 GOSUB 62
117 RETURN
118 REM OBJECTS AND LOCATIONS
119 REM =====================
120 REM OBJECTS
121 OC = 2 : REM OBJECT COUNT
122 DIM OB$(OC)
123 OB$(0)="MATCHES"
124 OB$(1)="BRASS KEY"
125 REM OBJECT DESCRIPTIONS
126 DIM OD$(OC)
127 OD$(0)="A SMALL BOOK OF PROMOTIONAL MATCHES ADVERTISING PATTY'S BAR AND GRILL, NORTH LAKES"
128 OD$(1)="A LARGE AND HEAVY KEY MADE OUT OF BRASS."
129 REM LOCATIONS
130 RC = 3 : REM ROOM COUNT
131 DIM LO$(RC)
132 LO$(0)="INVENTORY"
133 LO$(1)="DANK BASEMENT"
134 LO$(2)="FURNACE ROOM"
135 LO$(3)="SERVICE HATCH"
136 REM ROOM DESCRIPTIONS
137 DIM RD$(RC)
138 RD$(0)=""
139 RD$(1)="A CHILLINGLY DAMP, BARE-BRICKED ROOM WITH POURED CEMENT FLOOR AND TIMBER BEAMED CEILING. WINDOW FRAMES ARE BOARDED ALONG ONE WALL."
140 RD$(2)="THIS ROOM IS OBVIOUSLY A LATER ADDITION, THROWN TOGETHER WITH DRYWALL, AND JUST LARGE ENOUGH TO SECTION OFF THE FURNACE FROM THE MAIN BASEMENT."
141 RD$(3)="UP ABOVE THE FURNACE, THIS TINY SPACE MUST HAVE BEEN BUILT TO ALLOW ACCESS TO HVAC DUCTING."
142 REM OBJECT'S LOCATIONS
143 REM LOC 0 = PLAYER'S INVENTORY
144 DIM OL(OC)
145 OL(0)=0
146 OL(1)=1
147 REM EXIT NAMES
148 DIM EN$(6)
149 EN$(0)="NORTH"
150 EN$(1)="EAST"
151 EN$(2)="SOUTH"
152 EN$(3)="WEST"
153 EN$(4)="UP"
154 EN$(5)="DOWN"
155 REM ROOM EXITS
156 REM N E S W U D
157 DIM EX$(20)
158 EX$(1)="000002000000"
159 EX$(2)="010000000300"
160 EX$(3)="000000000002"
161 REM PLAYER
162 REM ======
163 PL = 1 : REM PLAYER LOCATION
164 PP = 1 : REM PLAYER PREVIOUS LOCATION
165 POKE 53281,6 : POKE 53280,6
166 PRINT "{CLR}{WHITE}"
167 PRINT " MURDER HOUSE"
168 PRINT " A TEXT ADVENTURE GAME"
169 PRINT " BY CHRIS GARRETT"
170 PRINT " 2024"
171 PRINT""
172 PRINT "{GREY3}"
173 PRINT " RETROGAMECODERS.COM"
174 PRINT""
175 PRINT""
176 PRINT""
177 GOSUB 62
178 RETURN
179 POKE 53281,6 : POKE 53280,14
180 PRINT "{CLR}{LIGHTBLUE}GOODBYE!"
181 PRINT ""
182 PRINT FRE(0)
183 PRINT ""